plugins {
    id 'de.aaschmid.cpd' version '1.0'
    id 'eclipse'
    id 'java'
    id 'findbugs'
}

archivesBaseName = 'taskwarrior-java-client'
description = 'A Java client to communicate with a taskwarrior server (= taskd).'
group = 'de.aaschmid'
version = '1.0-SNAPSHOT'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

jar {
    manifest {
        def title = 'Taskwarrior Java client'
        def vendor = 'Andreas Schmid'
        def copyright = "${new Date()[Calendar.YEAR]} ${vendor}"

        attributes (
            'Built-By': "Gradle ${gradle.gradleVersion}",
            'Built-Date': new Date(), // TODO destroys incremental build feature
            'Specification-Title': title,
            'Specification-Version': version,
            'Specification-Vendor': vendor,
            'Implementation-Title': title,
            'Implementation-Version': version,
            'Implementation-Vendor': vendor,
            'Issue-Tracker': 'https://github.com/aaschmid/taskwarrior-java-client/issues',
            'Documentation-URL': 'https://github.com/aaschmid/taskwarrior-java-client',
            'Copyright': copyright,
            'License': 'Apache License v2.0, January 2004'
        )
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

cpd {
    toolVersion = '5.5.1'
}

findbugs {
    toolVersion = '3.0.1'
    ignoreFailures = true
}

cpdCheck {
    ignoreFailures = true
    minimumTokenCount = 25
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}
